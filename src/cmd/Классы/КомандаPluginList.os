#Использовать "../../core"

Перем Лог;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("q quiet", Ложь, "вывод только названий плагинов");
	Команда.Опция("a all", Ложь, "выводит все установленные плагины");

	// Команда.Спек = "[-q | --quiet] ((-a | --all) | [NAME])";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ВывестиВсеПлагины	= Команда.ЗначениеОпции("all");
	ВыводТолькоНазваний	= Команда.ЗначениеОпции("quiet");

	МенеджерПлагинов = ПараметрыПриложения.МенеджерПлагинов();
	ВсеПлагины = МенеджерПлагинов.ПолучитьИндексПлагинов();
	
	Если НЕ ВыводТолькоНазваний Тогда
		Сообщить(СтрШаблон("Каталог плагинов: <%1>", ПараметрыПриложения.КаталогПлагинов()));
		Сообщить("Список плагинов: ");
	КонецЕсли;

	Для каждого КлючЗначение Из ВсеПлагины Цикл
		
		Плагин = КлючЗначение.Значение;

		Если НЕ ВывестиВсеПлагины Тогда 
			Если НЕ Плагин.Включен() Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		Если ВыводТолькоНазваний Тогда
			
			Сообщить(Плагин.Имя());
		
		Иначе
		
			ВывестиОписаниеПлагина(Плагин);

		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиОписаниеПлагина(КлассПлагина);

	ШаблонНаименования =  "%4 [%3] - %1 - %2";
	ТекстВключен = ?(КлассПлагина.Включен(), " [on]", "[off]");

	Сообщить(СтрШаблон(ШаблонНаименования, КлассПлагина.Имя(), КлассПлагина.Описание(), КлассПлагина.Версия(), ТекстВключен));

КонецПроцедуры

Лог = ПараметрыПриложения.Лог();