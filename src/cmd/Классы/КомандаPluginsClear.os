#Использовать "../../core"
#Использовать fs

Перем Лог;
Перем КаталогиПредустановленныхПлагинов;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("a all", Ложь, "удаляет в том числе предустановленные плагины");

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Если ПараметрыПриложения.ЭтоСборкаEXE() Тогда
		
		Лог.Информация("Очистка плагинов для собранного приложения в exe не доступна");

		Возврат;

	КонецЕсли;

	УдалитьПредустановленныеПлагины	= Команда.ЗначениеОпции("-a");
	
	КаталогПлагинов = ПараметрыПриложения.КаталогПлагинов();
	
	Если УдалитьПредустановленныеПлагины Тогда
		ФС.ОбеспечитьПустойКаталог(КаталогПлагинов);
	Иначе

		МассивКаталогов = НайтиФайлы(КаталогПлагинов, ПолучитьМаскуВсеФайлы());

		Для каждого ФайлКаталога Из МассивКаталогов Цикл
			
			Если НЕ ФайлКаталога.ЭтоКаталог() Тогда
				Продолжить;
			КонецЕсли;

			Если ЭтоКаталогПредустановленныхПлагинов(ФайлКаталога.ИмяБезРасширения) Тогда
				Продолжить;
			КонецЕсли;

			УдалитьФайлы(ФайлКаталога.ПолноеИмя);

		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоКаталогПредустановленныхПлагинов(Знач ИмяБезРасширения)

	ИмяБезРасширения = НРег(ИмяБезРасширения);

	Возврат Не КаталогиПредустановленныхПлагинов.Найти(ИмяБезРасширения) = Неопределено;
	
КонецФункции

КаталогиПредустановленныхПлагинов = ПараметрыПриложения.ПредустановленныеПлагины();
